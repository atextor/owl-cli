plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'com.adarshr.test-logger' version '3.0.0' apply false
    id 'io.freefair.lombok' version '5.3.3.3' apply false
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id 'org.ajoberstar.grgit' version '4.1.0'
}

allprojects {
    repositories {
        jcenter()
    }
}

def currentTag = {
    def grgit = grgit.open{ dir = "${projectDir}" }
    def tag = grgit.describe{ tags = true }
    if (tag != null) {
        tag.startsWith('snapshot') ? 'snapshot' : tag.replace('v', '')
    } else {
        'snapshot'
    }
}

subprojects {
    version = currentTag()
    task allDeps(type: DependencyReportTask) {}
    ext {
        homeDir = System.getProperty("user.home")
        lombokAnnotationProcessor = 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'
        lombokClaimingProcessor = 'lombok.launch.AnnotationProcessorHider$ClaimingProcessor'
        picocliProcessor = 'picocli.codegen.aot.graalvm.processor.NativeImageConfigGeneratorProcessor'
    }
}

apply plugin: 'jacoco'
apply plugin: 'java'

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    classDirectories.from = [
        "${projectDir}/cli/build/classes/java/main",
        "${projectDir}/diagram/build/classes/java/main",
        "${projectDir}/write/build/classes/java/main",
    ]
    sourceDirectories.from = [
        "${projectDir}/cli/src/main/java",
        "${projectDir}/diagram/src/main/java",
        "${projectDir}/write/src/main/java",
    ]

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination(file("${buildDir}/reports/jacoco/report.xml"))
        csv.enabled = false
    }
}

defaultTasks 'test', 'shadowJar'

def isNonStable = { String version ->
    return ['ALPHA', 'BETA', 'RC', 'M'].any { it -> version.toUpperCase().contains(it) }
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
