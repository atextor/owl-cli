import org.apache.tools.ant.filters.*

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow'
    id 'io.franzbecker.gradle-lombok'
    id 'com.adarshr.test-logger'
}

repositories {
    maven { url 'https://jitpack.io' }
}

apply from: file("${rootDir}/dependencies.gradle")
dependencies {
    implementation project(':diagram')
    implementation(deps.owlapi)
    implementation(deps.vavr)
    implementation(deps.jcommander)
    implementation(deps.picocli)
    annotationProcessor(deps.picocli_codegen)
    annotationProcessor(deps.jabel)

    // Override transitive dependency versions due to features
    implementation(deps.caffeine)

    // Override transitive dependency versions due to vulns
    implementation(deps.guava)
    implementation(deps.jackson_databind)
    implementation(deps.rdf4j_util)
    implementation(deps.rdf4j_rio_rdfxml)
    implementation(deps.rdf4j_rio_trix)
    implementation(deps.httpclient)
    implementation(deps.commons_codec)

    // Test
    testImplementation(deps.junit_jupiter_api)
    testImplementation(deps.junit_jupiter_params)
    testImplementation(deps.commons_io)
    testImplementation(deps.assertj)
    testImplementation(deps.classgraph)
    testRuntimeOnly(deps.junit_jupiter_engine)
}

shadowJar {
    archiveBaseName = 'owl-cli'
    archiveClassifier = null
}

task nativeImage(type: Exec, dependsOn: "shadowJar") {
    commandLine 'native-image',
      "-H:ConfigurationFileDirectories=${project.projectDir}/native-image,${buildDir}/classes/java/main/META-INF/native-image/picocli-generated/owl-cli/cli",
      '--initialize-at-build-time=uk.ac.manchester.cs.owl.owlapi.OWLOntologyManagerImpl',
      '--initialize-at-build-time=org.slf4j.impl.SimpleLogger',
      '--initialize-at-build-time=org.slf4j.LoggerFactory',
      '--initialize-at-build-time=org.slf4j.impl.StaticLoggerBinder',
      '-H:+TraceClassInitialization',
      '-H:+TraceServiceLoaderFeature',
      '-H:-UseServiceLoaderFeature',
      '-H:+AllowIncompleteClasspath',
      '--no-fallback',
      '--report-unsupported-elements-at-runtime',
      '-jar', "${buildDir}/libs/owl-cli-${version}.jar",
      "${buildDir}/bin/owl"
    inputs.file file("${buildDir}/libs/owl-cli-${version}.jar")
    outputs.file file("${buildDir}/bin/owl")
}

processResources {
    filter ReplaceTokens, tokens: [
        "application.version": version,
        "application.buildDate": new Date().format("yyyy-MM-dd HH:mm:ss")
    ]
}

compileJava {
    options.compilerArgs += ["--release", "11", "-Aproject=${project.group}/${project.name}"]
    sourceCompatibility = 13
    targetCompatibility = 11
}

compileTestJava {
    options.compilerArgs += ["--release", "11"]
    sourceCompatibility = 13
    targetCompatibility = 11
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
    ignoreFailures = false
    failFast = true

    filter {
        includeTestsMatching "*Test"
    }
}

mainClassName = 'de.atextor.owlcli.OWLCLI'
