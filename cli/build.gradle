buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'lt.neworld.jdeploy' version '0.6.0'
    id 'io.franzbecker.gradle-lombok' version '3.3.0'
    id 'com.adarshr.test-logger'
}

dependencies {
    implementation project(':diagram')
    implementation 'net.sourceforge.owlapi:owlapi-distribution:5.1.13'
    implementation 'io.vavr:vavr:0.10.2'
    implementation 'com.beust:jcommander:1.78'

    // Override transitive dependency versions due to vulns
    implementation 'com.google.guava:guava:28.2-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'
    implementation 'org.eclipse.rdf4j:rdf4j-util:3.1.3'
    implementation 'org.eclipse.rdf4j:rdf4j-rio-rdfxml:3.1.3'
    implementation 'org.eclipse.rdf4j:rdf4j-rio-trix:3.1.3'
    implementation 'org.apache.httpcomponents:httpclient:4.5.12'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

shadowJar {
    archiveBaseName = 'owl-cli'
    classifier = null
}

compileJava {
    options.compilerArgs += ["--enable-preview", "-Xlint:preview"]
    sourceCompatibility = 13
    targetCompatibility = 13
}

compileTestJava {
    options.compilerArgs += ["--enable-preview", "-Xlint:preview"]
}

test {
    useJUnitPlatform()
    jvmArgs '--enable-preview'
    maxHeapSize = '1G'
    ignoreFailures = false
    failFast = true

    filter {
        includeTestsMatching "*Test"
    }
}

jdeploy {
    name = "owl-cli"
    binName = "owl"
    author = "Andreas Textor"
    description = "Command line tool for ontology engineering"
    license = "LGPL"
    repository = "https://github.com/atextor/owl-cli"

    options {
        allowGlobalInstall = true
    }
}

mainClassName = 'de.atextor.owlcli.App'
